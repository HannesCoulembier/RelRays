#version 430
layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#define FLT_MAX 3.402823466e+38
#define FLT_MIN 1.175494351e-38

struct ColorSpectraDescription {
    
    int     InfraredSpectrumIndex;
    int     InfraredSpectrumSize;
    float   InfraredSpectrumMinWaveLength;
    float   InfraredSpectrumMaxWaveLength;

    int     RedSpectrumIndex;
    int     RedSpectrumSize;
    float   RedSpectrumMinWaveLength;
    float   RedSpectrumMaxWaveLength;

    int     GreenSpectrumIndex;
    int     GreenSpectrumSize;
    float   GreenSpectrumMinWaveLength;
    float   GreenSpectrumMaxWaveLength;

    int     BlueSpectrumIndex;
    int     BlueSpectrumSize;
    float   BlueSpectrumMinWaveLength;
    float   BlueSpectrumMaxWaveLength;

    vec4    RelativeWeights;
};
struct ObjectDescription {
    int LatestFragmentIndex; // Lowest index (the newest object fragments have the highest indices)
    int NumberOfFragments;

    int FirstIndexIndex;
    int IndexCount;
};
struct ObjectFragment {
    vec3 Pos;
    int MaterialIndex;
};
struct Material {
	vec4 Albedo;
	// float Roughness;
	float Metallic;
	vec4 EmissionColor;
	float EmissionStrength;
    ColorSpectraDescription AbsorptionSpectraDescription;
};
struct Vertex {
    vec3 Position;
};

layout(         binding = 0, rgba8) uniform image2D colorBuffer;
layout(         binding = 1)        uniform         CameraBlockAndScene {
    mat4 view;
    mat4 proj;
    mat4 invView;
    mat4 invProj;
    float uTime;
} cameraAndScene;
layout(         binding = 2)        uniform         RenderSettingsBlock     {
    int RayBounces;
} RenderSettings;
layout(         binding = 3)        uniform         CameraBlock             {
    ColorSpectraDescription ColorSpectraDescription;
} Camera;
layout(std140,  binding = 4)        readonly buffer ObjectDescriptionBlock  { ObjectDescription objectDescriptions[]; };
layout(std140,  binding = 5)        readonly buffer ObjectFragmentBlock     { ObjectFragment objectFragments[]; };
layout(std140,  binding = 6)        readonly buffer MaterialBlock           { Material materials[]; };
layout(         binding = 7)        readonly buffer SpectrumBlock           { float spectra[]; };
layout(         binding = 8)        readonly buffer VertexBufferBlock       { Vertex vertices[]; };
layout(         binding = 9)        readonly buffer IndexBufferBlock        { int indices[]; };

const int raySpectrumSampleCount = 100;
float rayMinWaveLength = 0.0;
float rayMaxWaveLength = 1800.0;
float pathAccLight[raySpectrumSampleCount];
float pathAccAbsorbed[raySpectrumSampleCount];

struct Ray {
    vec3 Origin;
    vec3 Direction;
};

struct HitPayload {
    float HitDistance;
    vec3 WorldPos;
    vec3 WorldNormal;
    int ObjectDescriptionIndex;
    int ObjectFragmentIndex;
};

struct SmallHitPayload {
    float HitDistance;
};

HitPayload TraceRay(Ray ray);
SmallHitPayload CalculateHitDistance(Ray ray, vec3 pos1, vec3 pos2, vec3 pos3);
HitPayload ClosestHit(Ray ray, float hitDistance, int objectDescriptionIndex, int objectFragmentIndex, int firstTriangleIndexIndex);
HitPayload Miss(Ray ray);

float SampleSpectra(float waveLength, float spectrumMinWaveLength, float spectrumMaxWaveLength, int spectrumStartIndex, int spectrumLength);
float SampleColorSpectraInfrared(float waveLength, ColorSpectraDescription colorSpectra);
float SampleColorSpectraRed(float waveLength, ColorSpectraDescription colorSpectra);
float SampleColorSpectraGreen(float waveLength, ColorSpectraDescription colorSpectra);
float SampleColorSpectraBlue(float waveLength, ColorSpectraDescription colorSpectra);

float IntegrateRayLightSpectrumWithInfraredSpectrum(ColorSpectraDescription colorSpectra);
float IntegrateRayLightSpectrumWithRedSpectrum(ColorSpectraDescription colorSpectra);
float IntegrateRayLightSpectrumWithGreenSpectrum(ColorSpectraDescription colorSpectra);
float IntegrateRayLightSpectrumWithBlueSpectrum(ColorSpectraDescription colorSpectra);

void main() {

    // Initializing global vars
    for (int i = 0; i < raySpectrumSampleCount; i++) {
        pathAccLight[i] = 0.0;
        pathAccAbsorbed[i] = 1.0;
    }

    ivec2 screenPos = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
    ivec2 screenSize = imageSize(colorBuffer);
	vec2 uv = vec2(float(screenPos.x) / float(screenSize.x), float(screenPos.y) / float(screenSize.y)); // uv with 0,0 in bottom left corner
	vec2 coord = uv * 2.0f - 1.0f; // -1 -> 1

    // Scene stuff
    vec3 lightDirection = normalize(vec3(5.0, 1.0, 0.0));
    vec3 cameraPos = vec3(cameraAndScene.view[3][0], cameraAndScene.view[3][1], cameraAndScene.view[3][2]);

    Ray ray;
    ray.Origin = cameraPos;
    vec4 target = cameraAndScene.invProj * vec4(coord.x, coord.y, 1, 1);
    ray.Direction = vec3(cameraAndScene.invView * vec4(normalize(vec3(target) / target.w), 0)); // World space

	for (int i = 0; i < RenderSettings.RayBounces; i++) {

		HitPayload payload = TraceRay(ray);
		if (payload.HitDistance < 0.0) { // HitDistance < 0.0 means it missed
            
            vec4 skyLight;

            // if (ray.Origin != cameraPos) {
            //     skyLight = 2.0 * vec4(0.0, 0.1, 0.2, 0.4) * (0.01 + max(0.0, dot(lightDirection, ray.Direction)));
            // }
            // else {
            //     skyLight = 2.0 * vec4(0.0, 0.1, 0.2, 0.4) * (0.50 + max(0.0, dot(lightDirection, ray.Direction)));
            // }

            // skyLight = vec4(0.0, 0.1, 0.2, 0.4);
            skyLight = vec4(0.01, 0.01, 0.01, 0.01);
            // skyLight = vec4(0.0, 0.0, 0.0, 0.0);

            for (int i = 0; i < raySpectrumSampleCount; i++) {
                float waveLength = rayMinWaveLength + (rayMaxWaveLength - rayMinWaveLength) * float(i) / raySpectrumSampleCount;
                ColorSpectraDescription spectraDescription = materials[0].AbsorptionSpectraDescription;
                float irFactor  = SampleColorSpectraInfrared(waveLength, spectraDescription) * skyLight.r;
                float rFactor   = SampleColorSpectraRed(waveLength, spectraDescription)      * skyLight.g;
                float gFactor   = SampleColorSpectraGreen(waveLength, spectraDescription)    * skyLight.b;
                float bFactor   = SampleColorSpectraBlue(waveLength, spectraDescription)     * skyLight.a;
                pathAccLight[i] += (irFactor + rFactor + gFactor + bFactor) * pathAccAbsorbed[i];
            }
			break;
        }

        ObjectDescription objectDescription = objectDescriptions[payload.ObjectDescriptionIndex];
        ObjectFragment objectFragment = objectFragments[payload.ObjectFragmentIndex];
        Material mat = materials[objectFragment.MaterialIndex];
        
        for (int i = 0; i < raySpectrumSampleCount; i++) {
            float waveLength = rayMinWaveLength + (rayMaxWaveLength - rayMinWaveLength) * float(i) / raySpectrumSampleCount;
            
            ColorSpectraDescription spectraDescription = mat.AbsorptionSpectraDescription;
            float irFactorAbs  = SampleColorSpectraInfrared(waveLength, spectraDescription) * mat.Albedo.r;
            float rFactorAbs   = SampleColorSpectraRed(waveLength, spectraDescription)      * mat.Albedo.g;
            float gFactorAbs   = SampleColorSpectraGreen(waveLength, spectraDescription)    * mat.Albedo.b;
            float bFactorAbs   = SampleColorSpectraBlue(waveLength, spectraDescription)     * mat.Albedo.a;

            float irFactorEmit  = SampleColorSpectraInfrared(waveLength, spectraDescription) * mat.EmissionColor.r;
            float rFactorEmit   = SampleColorSpectraRed(waveLength, spectraDescription)      * mat.EmissionColor.g;
            float gFactorEmit   = SampleColorSpectraGreen(waveLength, spectraDescription)    * mat.EmissionColor.b;
            float bFactorEmit   = SampleColorSpectraBlue(waveLength, spectraDescription)     * mat.EmissionColor.a;

            pathAccLight[i] += (irFactorEmit + rFactorEmit + gFactorEmit + bFactorEmit) * mat.EmissionStrength * pathAccAbsorbed[i];

            pathAccAbsorbed[i] *= (irFactorAbs + rFactorAbs + gFactorAbs + bFactorAbs) * (1.0 - mat.Metallic);
        }

		ray.Origin = payload.WorldPos + payload.WorldNormal * 0.0001f;
        ray.Direction = normalize(reflect(ray.Direction, payload.WorldNormal));
	}

    ColorSpectraDescription spectraDescription = Camera.ColorSpectraDescription;
    float ir    = IntegrateRayLightSpectrumWithInfraredSpectrum(spectraDescription) * spectraDescription.RelativeWeights.r;
    float r     = IntegrateRayLightSpectrumWithRedSpectrum(spectraDescription)      * spectraDescription.RelativeWeights.g;
    float g     = IntegrateRayLightSpectrumWithGreenSpectrum(spectraDescription)    * spectraDescription.RelativeWeights.b;
    float b     = IntegrateRayLightSpectrumWithBlueSpectrum(spectraDescription)     * spectraDescription.RelativeWeights.a;
    vec3 finalColor = vec3(ir + r,g,b);

    // if (uv.y < 0.2) { // Shows "location" of red green and blue spectra
    //     float waveLength = uv.x * 1800.0;
    //     r = SampleColorSpectraRed(waveLength, materials[0].ColorSpectraDescription);
    //     g = SampleColorSpectraGreen(waveLength, materials[0].ColorSpectraDescription);
    //     b = SampleColorSpectraBlue(waveLength, materials[0].ColorSpectraDescription);
    //     finalColor = vec3(r, g, b);
    // }

    // if (uv.y < 0.1) { // Shows how all wavelengths are percieved by current camera
    //     float waveLength = uv.x * 1800.0;
    //     r = SampleColorSpectraRed(waveLength, spectraDescription)   * spectraDescription.RelativeWeights.g;
    //     g = SampleColorSpectraGreen(waveLength, spectraDescription) * spectraDescription.RelativeWeights.b;
    //     b = SampleColorSpectraBlue(waveLength, spectraDescription)  * spectraDescription.RelativeWeights.a;
    //     finalColor = vec3(r, g, b);
    // }

    imageStore(colorBuffer, screenPos, vec4(finalColor, 1.0));
}

HitPayload TraceRay(Ray ray) {

	int closestObjectDescription = -1;
    int closestObjectFragment = -1;
    int closestTriangleIndexIndex = -1;

	float hitDistance = FLT_MAX;

	for (int i = 0; i < objectDescriptions.length(); i++) {
        
        ObjectDescription objectDescription = objectDescriptions[i];

        for (int j = 0; j < objectDescription.NumberOfFragments; j++) {

            ObjectFragment objectFragment = objectFragments[objectDescription.LatestFragmentIndex + j];

            for (int k = 0; k < (objectDescription.IndexCount) / 3; k++) { // Count up every 3 indices (per triangle)

                Vertex v1 = vertices[indices[objectDescription.FirstIndexIndex + k * 3 + 0]];
                Vertex v2 = vertices[indices[objectDescription.FirstIndexIndex + k * 3 + 1]];
                Vertex v3 = vertices[indices[objectDescription.FirstIndexIndex + k * 3 + 2]];

                vec3 p1 = v1.Position + objectFragment.Pos;
                vec3 p2 = v2.Position + objectFragment.Pos;
                vec3 p3 = v3.Position + objectFragment.Pos;

                SmallHitPayload smallPayload = CalculateHitDistance(ray, p1, p2, p3);
                float t = smallPayload.HitDistance;
                if (t > 0.0 && t < hitDistance) {
                    hitDistance = t;
                    closestObjectDescription = i;
                    closestObjectFragment = objectDescription.LatestFragmentIndex + j;
                    closestTriangleIndexIndex = objectDescription.FirstIndexIndex + k * 3;
                }
            }
        }

        // vec3 origin = ray.Origin - objectFragment.Pos;

	    // float a = dot(ray.Direction, ray.Direction);
	    // float b = 2.0f * dot(ray.Direction, origin);
	    // float c = dot(origin, origin) - objectDescription.Radius * objectDescription.Radius;

	    // float discriminant = b * b - 4 * a * c;

	    // if (discriminant < 0)
		//     continue;
	
	    // float t = (-b - sqrt(discriminant)) / (2.0f * a);
	    
        // if (t > 0.0f && t < hitDistance) {
        //     hitDistance = t;
	    //     closestObjectDescription = i;
        //     closestObjectFragment = objectDescription.LatestFragmentIndex;
        // }
    }
    
	if (closestObjectDescription < 0) {
		return Miss(ray);
    }

	return ClosestHit(ray, hitDistance, closestObjectDescription, closestObjectFragment, closestTriangleIndexIndex);
}

bool PointInTriangle(vec3 p, vec3 p0, vec3 p1, vec3 p2) {
  vec3 a = p0 - p;
  vec3 b = p1 - p;
  vec3 c = p2 - p;

  vec3 u = cross(b, c);
  vec3 v = cross(c, a);
  vec3 w = cross(a, b);

  if (dot(u, v) < 0.0) {
      return false;
  }
  if (dot(u, w) < 0.0) {
      return false;
  }

  return true;
}

SmallHitPayload CalculateHitDistance(Ray ray, vec3 pos1, vec3 pos2, vec3 pos3) {
    SmallHitPayload result;
    vec3 normal = normalize(cross(pos2 - pos1, pos3 - pos1));

    float t = dot(normal, (pos1 - ray.Origin)) / dot(normal, ray.Direction);
    
    // Check if hitPoint is in triangle
    vec3 hitPos = ray.Origin + t * ray.Direction;
    bool isInTriangle = PointInTriangle(hitPos, pos1, pos2, pos3);
    if (isInTriangle)
        result.HitDistance = t;
    else
        result.HitDistance = -1.0;
    return result;
}

HitPayload Miss(Ray ray) {
    
    HitPayload payload;
    payload.HitDistance = -1.0;
    payload.WorldPos = vec3(0.0);
    payload.WorldNormal = ray.Direction;
    payload.ObjectDescriptionIndex = -1;
    payload.ObjectFragmentIndex = -1;
    return payload;
}

HitPayload ClosestHit(Ray ray, float hitDistance, int objectDescriptionIndex, int objectFragmentIndex, int firstTriangleIndexIndex) {
    
    ObjectDescription objectDescription = objectDescriptions[objectDescriptionIndex];
    ObjectFragment objectFragment = objectFragments[objectFragmentIndex];
    Vertex v1 = vertices[indices[firstTriangleIndexIndex + 0]];
    Vertex v2 = vertices[indices[firstTriangleIndexIndex + 1]];
    Vertex v3 = vertices[indices[firstTriangleIndexIndex + 2]];

    vec3 planeNormal = cross((v2.Position - v1.Position), (v3.Position - v1.Position));

    HitPayload payload;

	payload.HitDistance = hitDistance;
	payload.WorldPos = ray.Origin + ray.Direction * hitDistance;
    // payload.WorldNormal = normalize(payload.WorldPos - objectFragment.Pos); // For a sphere
	payload.WorldNormal = normalize(-planeNormal * dot(ray.Direction, planeNormal)); // Multiply by dot of ray direction and normal to get the normal to face the right way, then normalize
    payload.ObjectDescriptionIndex = objectDescriptionIndex;
    payload.ObjectFragmentIndex = objectFragmentIndex;

    return payload;
}


float SampleSpectra(float waveLength, float spectrumMinWaveLength, float spectrumMaxWaveLength, int spectrumStartIndex, int spectrumLength) {
    
    float virtualIndex = float(spectrumStartIndex) + float(spectrumLength) * (waveLength - spectrumMinWaveLength) / (spectrumMaxWaveLength - spectrumMinWaveLength);
    int lowerIndex = int(virtualIndex);
    int upperIndex = lowerIndex + 1;
    if (lowerIndex < float(spectrumStartIndex)) {
        if (upperIndex < float(spectrumStartIndex)) {
            return 0.0;
        }
        return spectra[upperIndex];
    }
    else if (upperIndex >= float(spectrumStartIndex + spectrumLength)) {
        if (lowerIndex >= float(spectrumStartIndex + spectrumLength)) {
            return 0.0;
        }
        return spectra[lowerIndex];
    }
    else {
        float lowerValue = spectra[lowerIndex];
        float upperValue = spectra[upperIndex];
        float lowerWeight = 1.0 - (virtualIndex - lowerIndex);
        float upperWeight = 1.0 - lowerWeight;
        return lowerValue * lowerWeight + upperValue * upperWeight;
    }
}

float SampleColorSpectraInfrared(float waveLength, ColorSpectraDescription colorSpectra) {
    
    return SampleSpectra(waveLength, colorSpectra.InfraredSpectrumMinWaveLength, colorSpectra.InfraredSpectrumMaxWaveLength, colorSpectra.InfraredSpectrumIndex, colorSpectra.InfraredSpectrumSize) * colorSpectra.RelativeWeights.r;
}

float SampleColorSpectraRed(float waveLength, ColorSpectraDescription colorSpectra) {
    
    return SampleSpectra(waveLength, colorSpectra.RedSpectrumMinWaveLength, colorSpectra.RedSpectrumMaxWaveLength, colorSpectra.RedSpectrumIndex, colorSpectra.RedSpectrumSize) * colorSpectra.RelativeWeights.g;
}

float SampleColorSpectraGreen(float waveLength, ColorSpectraDescription colorSpectra) {
    
    return SampleSpectra(waveLength, colorSpectra.GreenSpectrumMinWaveLength, colorSpectra.GreenSpectrumMaxWaveLength, colorSpectra.GreenSpectrumIndex, colorSpectra.GreenSpectrumSize) * colorSpectra.RelativeWeights.b;
}

float SampleColorSpectraBlue(float waveLength, ColorSpectraDescription colorSpectra) {
    
    return SampleSpectra(waveLength, colorSpectra.BlueSpectrumMinWaveLength, colorSpectra.BlueSpectrumMaxWaveLength, colorSpectra.BlueSpectrumIndex, colorSpectra.BlueSpectrumSize) * colorSpectra.RelativeWeights.a;
}

float IntegrateRayLightSpectrumWithInfraredSpectrum(ColorSpectraDescription colorSpectra) {
    
    float total = 0.0;
    for (int i = 0; i < raySpectrumSampleCount; i++) {
        float waveLength = rayMinWaveLength + (rayMaxWaveLength - rayMinWaveLength) * float(i) / raySpectrumSampleCount;
        total += pathAccLight[i] * SampleColorSpectraInfrared(waveLength, colorSpectra);
    }
    return total * (rayMaxWaveLength - rayMinWaveLength)/ raySpectrumSampleCount;
}

float IntegrateRayLightSpectrumWithRedSpectrum(ColorSpectraDescription colorSpectra) {
    
    float total = 0.0;
    for (int i = 0; i < raySpectrumSampleCount; i++) {
        float waveLength = rayMinWaveLength + (rayMaxWaveLength - rayMinWaveLength) * float(i) / raySpectrumSampleCount;
        total += pathAccLight[i] * SampleColorSpectraRed(waveLength, colorSpectra);
    }
    return total * (rayMaxWaveLength - rayMinWaveLength)/ raySpectrumSampleCount;
}

float IntegrateRayLightSpectrumWithGreenSpectrum(ColorSpectraDescription colorSpectra) {
    
    float total = 0.0;
    for (int i = 0; i < raySpectrumSampleCount; i++) {
        float waveLength = rayMinWaveLength + (rayMaxWaveLength - rayMinWaveLength) * float(i) / raySpectrumSampleCount;
        total += pathAccLight[i] * SampleColorSpectraGreen(waveLength, colorSpectra);
    }
    return total * (rayMaxWaveLength - rayMinWaveLength)/ raySpectrumSampleCount;
}

float IntegrateRayLightSpectrumWithBlueSpectrum(ColorSpectraDescription colorSpectra) {
    
    float total = 0.0;
    for (int i = 0; i < raySpectrumSampleCount; i++) {
        float waveLength = rayMinWaveLength + (rayMaxWaveLength - rayMinWaveLength) * float(i) / raySpectrumSampleCount;
        total += pathAccLight[i] * SampleColorSpectraBlue(waveLength, colorSpectra);
    }
    return total * (rayMaxWaveLength - rayMinWaveLength)/ raySpectrumSampleCount;
}